@echo off

:: Copyright (c) 2015-2019, RTE (http://www.rte-france.com)
:: See AUTHORS.txt
:: All rights reserved.
:: This Source Code Form is subject to the terms of the Mozilla Public
:: License, v. 2.0. If a copy of the MPL was not distributed with this
:: file, you can obtain one at http://mozilla.org/MPL/2.0/.
:: SPDX-License-Identifier: MPL-2.0
::
:: This file is part of Dynawo, a hybrid C++/Modelica open source time domain
:: simulation tool for power systems.

set DYNAWO_INSTALL_DIR=%~DP0

:: Dynawo environment variables for runtime
set DYNAWO_DDB_DIR=%DYNAWO_INSTALL_DIR%ddb
set DYNAWO_DICTIONARIES=dictionaries_mapping
set DYNAWO_LOCALE=en_GB
set DYNAWO_RESOURCES_DIR=%DYNAWO_INSTALL_DIR%share\
set DYNAWO_SCRIPTS_DIR=%DYNAWO_INSTALL_DIR%sbin
set DYNAWO_USE_XSD_VALIDATION=true
set DYNAWO_XSD_DIR=%DYNAWO_RESOURCES_DIR%xsd\

set thirdPartyInstallPath=@DYNAWO_THIRD_PARTY_DIR@
set LIBIIDM_HOME=%thirdPartyInstallPath%\libiidm
set IIDM_XML_XSD_PATH=%LIBIIDM_HOME%\share\iidm\xsd\

set DYNAWO_LIBIIDM_EXTENSIONS=%LIBIIDM_HOME%\bin

:: For DLL runtime
set LIBXML_HOME=%thirdPartyInstallPath%\libxml
set BOOST_PATH=%thirdPartyInstallPath%\boost
set LIBARCHIVE_HOME=%thirdPartyInstallPath%\libarchive
set ZLIB_ROOT=%thirdPartyInstallPath%\zlib
set LIBZIP_HOME=%thirdPartyInstallPath%\libzip
set ADEPT_HOME=%thirdPartyInstallPath%\adept
set SUITESPARSE_HOME=%thirdPartyInstallPath%\suitesparse
set SUNDIALS_HOME=%thirdPartyInstallPath%\sundials
set XERCESC_HOME=%thirdPartyInstallPath%\xerces-c
set LIBXML2_HOME=%thirdPartyInstallPath%\libxml2

set OPENMODELICA_HOME=@INSTALL_OPENMODELICA@

if not defined DYNAWO_PYTHON_COMMAND (
  set DYNAWO_PYTHON_COMMAND=python3
)

if not defined DYNAWO_BROWSER (
  setlocal

  :: setup a default browser in case we fail
  set default_browser=C:\Program Files\Internet Explorer\iexplore.exe

  :: look in the HKEY_CLASSES_ROOT\htmlfile\shell\open\command registry for the default browser
  for /f "tokens=*" %%a in ('REG QUERY HKEY_CLASSES_ROOT\htmlfile\shell\open\command /ve ^| FIND /i "default"') do (
    set input=%%a
  )
  setlocal enableDelayedExpansion

  :: parse the input field looking for the second token
  for /f tokens^=^2^ eol^=^"^ delims^=^" %%a in ("!input!") do set DYNAWO_BROWSER=%%a

  setlocal disableDelayedExpansion

  :: this may not be needed, check if reg returned a real file, if not unset browser
  if not "%DYNAWO_BROWSER%" == "" if not exist "%DYNAWO_BROWSER%" set DYNAWO_BROWSER=
  if "%DYNAWO_BROWSER%"=="" set "DYNAWO_BROWSER=%default_browser%"
  endlocal
)

set oldpath=%path%

PATH=%OPENMODELICA_HOME%\bin;%DYNAWO_INSTALL_DIR%ddb;%DYNAWO_INSTALL_DIR%bin;%LIBXML_HOME%\bin;%PATH%
PATH=%BOOST_PATH%\bin;%LIBARCHIVE_HOME%\bin;%ZLIB_ROOT%\bin;%LIBIIDM_HOME%\bin;%LIBZIP_HOME%\bin;%ADEPT_HOME%\bin;%SUITESPARSE_HOME%\bin;%SUNDIALS_HOME%\bin;%XERCESC_HOME%\bin;%LIBXML2_HOME%\bin;%PATH%

:: To compile Modelica models
set DYNAWO_ADEPT_INSTALL_DIR=%thirdPartyInstallPath%\adept
set DYNAWO_INSTALL_OPENMODELICA=%OPENMODELICA_HOME%
set OPENMODELICAHOME=%DYNAWO_INSTALL_OPENMODELICA%
set DYNAWO_XERCESC_INSTALL_DIR=%thirdPartyInstallPath%\xerces-c
set DYNAWO_SUITESPARSE_INSTALL_DIR=%thirdPartyInstallPath%\suitesparse
set DYNAWO_SUNDIALS_INSTALL_DIR=%thirdPartyInstallPath%\sundials
set DYNAWO_BOOST_HOME=%thirdPartyInstallPath%\boost
set DYNAWO_LIBXML_HOME=%thirdPartyInstallPath%\libxml

set "pathCurves=%~dp2"
set tmpVar=%pathCurves%outputs\curves\curvesOutput\curves.html
for %%i in ("%tmpVar%") do set "curvesHtml=%%~fi"

if /I "%~1"=="jobs" (
  "%DYNAWO_INSTALL_DIR%"bin\dynawo %2
) else (
  if /I "%~1"=="generate-preassembled" (
    for /f "tokens=1,* delims= " %%a in ("%*") do set GENERATE_PREASSEMBLED_ARGS=%%b
    "%DYNAWO_INSTALL_DIR%"sbin\generate-preassembled.exe !GENERATE_PREASSEMBLED_ARGS!
  ) else (
    if /I "%~1"=="dump-model" (
      for /f "tokens=1,* delims= " %%a in ("%*") do set DUMP_MODEL_ARGS=%%b
      "%DYNAWO_INSTALL_DIR%"sbin\dumpModel.exe !DUMP_MODEL_ARGS!
    ) else (
      if /I "%~1"=="jobs-with-curves" (
        "%DYNAWO_INSTALL_DIR%"bin\dynawo %2
        "%DYNAWO_PYTHON_COMMAND%" "%DYNAWO_INSTALL_DIR%\sbin\curvesToHtml\curvesToHtml.py" --jobsFile=%2 --withoutOffset --htmlBrowser="%DYNAWO_BROWSER%"
        "%DYNAWO_BROWSER%" "%curvesHtml%"
        echo.
        echo Open %curvesHtml% with your browser if it does not do it automatically.
        echo.
      ) else (
        if /I "%~1"=="curves" (
          "%DYNAWO_PYTHON_COMMAND%" "%DYNAWO_INSTALL_DIR%\sbin\curvesToHtml\curvesToHtml.py" --jobsFile=%2 --withoutOffset --htmlBrowser="%DYNAWO_BROWSER%"
          "%DYNAWO_BROWSER%" "%curvesHtml%"
          echo.
          echo Open %curvesHtml% with your browser if it does not do it automatically.
          echo.
        ) else (
          if /I "%~1"=="version" (
            "%DYNAWO_INSTALL_DIR%"bin\dynawo --version
          ) else (
            if /I "%~1"=="help" (
              call :dynawo_help
            ) else (
              call :dynawo_help
            )
          )
        )
      )
    )
  )
)

PATH=%oldpath%

exit /B %ERRORLEVEL%

setlocal

:dynawo_help
echo Usage: %~n0 ^<option^>
echo.
echo   where option is:
echo     jobs ^<jobs-file^>                 launch Dynawo simulation
echo     jobs-with-curves ^<jobs-file^>     launch Dynawo simulation and open resulting curves in a browser
echo     curves ^<jobs-file^>               open resulting curves in a browser
echo     generate-preassembled ^<options^>  generate a preassembled model (.dll) from a model description (.xml)
echo     dump-model ^<options^>             dump variables and parameters of a Dynawo model (.dll) into a xml file
echo     version                            show dynawo version
echo     help                               show this message
exit /B 0

endlocal
